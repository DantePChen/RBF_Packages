// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//-
// ==========================================================================
//
// ==========================================================================
//+

//  Description:	defines behaviour and layout of helixContext
//					tool property sheet
//


global proc helixProperties ()
//
//	Procedure Name:
//		helixProperties
//
//	Description:
//		layout of tool property sheet
//
//	Input Arguments:
//		None.
//
//	Return Value:
//		None.
//
{
	setUITemplate -pushTemplate DefaultTemplate;

	string $parent = `toolPropertyWindow -q -location`;
    setParent $parent;
	string $curctx = `currentCtx`;

	columnLayout helix;
		tabLayout -childResizable true helixTabs;
			columnLayout helixTab;
				frameLayout -cll true -cl false -l "Helix Options" helixFrame;
					columnLayout helixOptions;
						separator -style "none";

						intSliderGrp
							-field 1
							-label "Number of CVs"
							-minValue 20
							-maxValue 100
							-value 1
							numCVs;

						checkBoxGrp
							-label "Upside Down"
							-numberOfCheckBoxes 1
							-l1 " "
							upsideDownGrp;

					setParent ..; // helixOptions
				setParent ..; // helixFrame
			setParent ..; // helixTab
		setParent ..; // helixTabs
	setParent ..; // helix

	// Name the tabs; -tl does not allow tab labelling upon creation
	tabLayout -e -tl helixTab "Tool Defaults" helixTabs;

	setUITemplate -popTemplate;

	helixSetCallbacks($parent);
}


global proc helixSetCallbacks(string $parent)
//
//	Procedure Name:
//		helixSetCallbacks
//
//	Description:
//		associate control events with callbacks
//
//	Input Arguments:
//		parent name.
//
//	Return Value:
//		None.
//
{
	setParent	$parent;
	string		$whichCtx = `currentCtx`;

 	checkBoxGrp -e
 		-on1 ("helixToolContext -e -upsideDown true `currentCtx`")
 		-of1 ("helixToolContext -e -upsideDown false `currentCtx`")
 		upsideDownGrp;

 	intSliderGrp -e
 		-cc ("helixToolContext -e -numCVs #1 `currentCtx`")
 		numCVs;
}


