// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//-
// ==========================================================================
//
// ==========================================================================
//+

//
//  Description:
//     This script creates a new Maya menu that contains a
//	   single item "Formated Text". When selected, it will create
//     a beveled text with history, where the text can be the
//     formatted result of numerical inputs.
//
//  Procedures:
//     createFormatedText, addStringFormatMenuItems,
//     stringFormatCreateUI, stringFormatDeleteUI
//

//	Callback routine for the menu item
global proc createFormatedText()
{
	optionVar -intValue textCurvesType 3;
	performtextCurves(0);
	string $sel[] = `ls -sl`;

	// Create a circle node dependency object called "stringFormat"
	string $nodes = `createNode stringFormat`;
	connectAttr ($nodes+".output") ($sel[0]+".text");

	// Initialize with some values that do not produce an empty string
	setAttr -type "string" ($nodes+".format") "format ^0f ^0d ^0t ";
	setAttr ($nodes+".values[0]") 35.5 ;
}

// Add menu entries to the Create Light menu
//
global proc string addStringFormatMenuItems(string $parentMenu)
{
	string $menuName;
	string $res = "";

	$menuName = `menuItem -p $parentMenu -l "Formated Text"
		-ann "Creates a formated text node which can be animated"
		-c "createFormatedText" -i "text.png"` ;
	$res = $res + ";deleteUI -mi " + $menuName;

	return $res; 
}

// Add UI when plugin is loaded
///////////////////////////////////////////////////////////////////////////////
global string $gStrFMTCreateMenuVariable = "";
global proc stringFormatCreateUI()
{ 
	// Build the actual UI for all those items:
    global string $gMainCreateMenu;

	string $parent = $gMainCreateMenu;	// Create menu
	string $cmd = "addStringFormatMenuItems "+$parent;
	addMenuItemSafe($parent, $cmd, "gStrFMTCreateMenuVariable");
}

// Remove UI when plugin is unloaded
///////////////////////////////////////////////////////////////////////////////
global proc stringFormatDeleteUI()
{
    global string $gMainCreateMenu;

	string $parent = $gMainCreateMenu;	// Create menu
	removeMenuItemSafe($parent, "gStrFMTCreateMenuVariable");
}
